		ORG 0
		MOV R13,#1<16 					; INICIJALIZACIJA STOGA
		MRS R0,CPSR 					; OMOGUCAVANJE PREKIDA
		AND R0,R0,#%B 01111111
		MSR CPSR_c,R0 
		B GLAVNI
		
		ORG 18 							; PP ZA RTC - U R0 RTC, U R1 GPIO
		STMFD R13!,{R2,R3,R4,R14}  		; KONTEKST - SPREMA SE I R14 ZBOG UGNJEZDENIH POZIVA
		STR R2,[R0,#8] 					; UKLANJANJE ZAHTJEVA ZA PREKID
		MOV R2,#0 						; RESET BROJILA
		STR R2,[R0,#%D12]
		MOV R2,#0D 
		BL LCDWR 						; BRISANJE REGISTRA LCD-A
		LDR R3,INDEKS 					; DOHVAT REDNOG BROJA ZNAKA
		ADD R3,R3,#4<8 					; DOHVAT TOCNE ADRESE ZNAKA
		LDRB R2,[R3] 					; DOHVAT ZNAKA
		CMP R2,#0 						; AKO JE NULA PRESKOCI ISPIS
		BEQ DALJE
										; ISPIS >>>
		MOV R2,#3E 						; ZNAK >
		MOV R4,#3 						; BROJAC

PRVI	BL LCDWR
		SUBS R4,R4,#1 
		BNE PRVI

										;ISPIS ZNAKA
		LDRB R2,[R3] 					; DOHVAT ZNAKA JER JE U R3 VEC TOCNA ADRESA
		BL LCDWR

										; ISPIS <<<
		MOV R4,#3 						; BROJAC
		MOV R2,#3C 						; ZNAK <

DRUGI	BL LCDWR
		SUBS R4,R4,#1
		BNE DRUGI
		MOV R2,#0A 						; PRIKAZ REGISTRA LCDA NA ZASLONU
		BL LCDWR
		SUB R3,R3,#4<8 					; VRIJEDNOST INDEKSA
		ADD R3,R3,#1 					; UVECAVA SE ZA 1
		STR R3,INDEKS
		B KRAJ
		
DALJE	MOV R3,#0 						; VRIJEDNOST INDEKSA
		STR R3,INDEKS 					; RESETIRA SE
		
KRAJ 	LDMFD R13!,{R2,R3,R4,R14} 		; OBNOVA KONTEKSTA
		SUBS PC,LR,#4 					; POVRATAK IZ PP
		
LCDWR	STMFD R13!,{R2} 				; KONTEKST; U R1 JE GPIO, U R2 ZNAK ZA UPIS
		AND R2,R2,#7F 					; ZA SVAKI SLUCAJ BRISEMO BIT 7
		STR R2,[R1,#4] 					; UPIS U REGISTAR PODATAKA B
		ORR R2,R2,#80 					; POSTAVLJANJE BITA 7
		STR R2,[R1,#4] 					; SLANJE IMPULSA ZA UPIS NA LCD
		AND R2,R2,#7F 					; BRISANJE BITA 7
		STR R2,[R1,#4]
		LDMFD R13!,{R2} 				; OBNOVA KONTEKSTA
		MOV PC,LR 						; POVRATAK
		
GLAVNI	LDR R0,RTC 						; ADRESA RTC-A
		LDR R1,GPIO 					; ADRESA GPIO
		MOV R2,#1<8 					; INICIJALIZACIJA REG USPOREDBE
		STR R2,[R0,#4]
		MOV R2,#1 						; DOZVOLJAVANJE PREKIDA RTC-A
		STR R2,[R0,#%D16]

LOOP	B LOOP 							; "KORISTAN POSAO"

RTC		DW 0FFFFFE00
GPIO	DW 0FFFFFF00
INDEKS	DW 0
		ORG 400
		DB 49,6e,74,65,72,6e,61,74,69,6f,6e,61,6c,69,73,61,74,69,6f,6e,0
